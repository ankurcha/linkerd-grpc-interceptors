plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'

group 'com.malloc64.grpc.linkerd'
version '1.0.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/ankurcha/maven" }
    jcenter()
}

dependencies {
    compile 'io.grpc:grpc-core:1.5.0'

    testCompile 'junit:junit:4.12'
    testCompile 'io.grpc:grpc-testing:1.5.0'
    testCompile 'io.grpc:grpc-testing-proto:1.5.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}


bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = 'linkerd-grpc-interceptors'
        desc = 'Linkerd header propagating interceptors for grpc'
        websiteUrl = 'https://github.com/ankurcha/linkerd-grpc-interceptors'
        issueTrackerUrl = 'https://github.com/ankurcha/linkerd-grpc-interceptors/issues'
        vcsUrl = 'https://github.com/ankurcha/linkerd-grpc-interceptors.git'
        licenses = ['Apache-2.0']
        labels = ['grpc', 'linkerd', 'interceptors', 'grpc-java']
        publicDownloadNumbers = true
        attributes = ['grpcVersion': '1.5.0']

        version {
            name = project.version
            released  = new Date()
            vcsTag = '1.0.0'
            attributes = ['grpcVersion': '1.5.0']
            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = 'ossUser' //OSS user token
                password = 'ossUserPassword' //OSS user password
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}
